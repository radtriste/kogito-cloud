@Library('jenkins-pipeline-shared-libraries')_

IMAGES = ["kogito-quarkus-ubi8", 
            "kogito-quarkus-jvm-ubi8",
            "kogito-quarkus-ubi8-s2i",
            "kogito-springboot-ubi8",
            "kogito-springboot-ubi8-s2i",
            "kogito-data-index",
            "kogito-jobs-service",
            "kogito-management-console"
         ]


deployProperties = [ : ]


pipeline {
    agent {
        label 'kogito-image-slave && !master'
    }

    // Needed for local build
    tools {
        jdk 'kie-jdk11'
    }

    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')

        // Git information
        // TODO to change back to master
        string(name: 'BUILD_BRANCH_NAME', defaultValue: 'release_pipeline', description: 'Which branch to build ? Set if you are not on a multibranch pipeline.')
        // TODO to change back to kiegroup
        string(name: 'GIT_AUTHOR', defaultValue: 'radtriste', description: 'Which Git author repository ?')
        
        // Build&Test information
        booleanParam(name: 'SKIP_TESTS', defaultValue: false, description: 'Skip tests')
        string(name: 'MAVEN_ARTIFACT_REPOSITORY', defaultValue: '', description: 'Maven repository where the build artifacts are present')

        // Deploy information
        booleanParam(name: 'IMAGE_USE_OPENSHIFT_REGISTRY', defaultValue: false, description: 'Set to true if image should be deployed in Openshift registry.In this case, IMAGE_REGISTRY_CREDENTIALS, IMAGE_REGISTRY and IMAGE_NAMESPACE parameters will be ignored')
        string(name: 'IMAGE_REGISTRY_CREDENTIALS', defaultValue: '', description: 'Image registry credentials to use to deploy images. Will be ignored if no IMAGE_REGISTRY is given')
        string(name: 'IMAGE_REGISTRY', defaultValue: '', description: 'Image registry to use to deploy images')
        string(name: 'IMAGE_NAMESPACE', defaultValue: 'kiegroup', description: 'Image namespace to use to deploy images')
        string(name: 'IMAGE_NAME_SUFFIX', defaultValue: '', description: 'Image name suffix to use to deploy images. In case you need to change the final image name, you can add a suffix to it.')
        string(name: 'IMAGE_TAG', defaultValue: '', description: 'Image tag to use to deploy images')
        
        // Release information
        booleanParam(name: 'RELEASE', defaultValue: false, description: 'Is this build for a release?')
        string(name: 'PROJECT_VERSION', defaultValue: '', description: 'Optional if not RELEASE. If RELEASE, cannot be empty.')
    }

    environment {
        JAVA_HOME = "${GRAALVM_HOME}"

        OPENSHIFT_API = credentials("OPENSHIFT_API")
        OPENSHIFT_REGISTRY = credentials("OPENSHIFT_REGISTRY")
        OPENSHIFT_CREDS_KEY = "OPENSHIFT_CREDS"

        // TODO to uncomment
        //BOT_CREDENTIALS_ID = "bsig-gh-bot"
        BOT_CREDENTIALS_ID = "radtriste-bot"

        // TODO to remove & where used
        AUTHOR_CREDS = "radtriste"
    }

    stages {
        stage('Initialization') {
            steps {
                script {  
                    clean()

                    if (params.DISPLAY_NAME != "") {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    // Set the mirror url only if no artifact repository is given
                    if (getMavenArtifactRepository() == '' 
                            && env.MAVEN_MIRROR_REPOSITORY != null
                            && env.MAVEN_MIRROR_REPOSITORY != ''){
                        echo "Set Maven mirror url"
                        env.MAVEN_MIRROR_URL = env.MAVEN_MIRROR_REPOSITORY
                    }

                    checkoutRepo()

                    if (isRelease()) {
                        assert getProjectVersion() != ''
                    }

                    setDeployPropertyIfNeeded('git.branch', getBuildBranch())
                    setDeployPropertyIfNeeded('git.author', getGitAuthor())
                    setDeployPropertyIfNeeded('project.version', getProjectVersion())
                    setDeployPropertyIfNeeded('release', isRelease())

                }
            }
        }
        stage('Prepare for PR') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    githubscm.forkRepo(env.BOT_CREDENTIALS_ID)
                    githubscm.createBranch(getProjectVersion())
                }
            }
        }
        stage('Update project version'){
            when {
                expression { return getProjectVersion() != '' }
            }
            steps {
                script{
                    versionCmd = "python3 scripts/manage-kogito-version.py --bump-to ${getProjectVersion()} --confirm"
                    if(getBuildBranch() != 'master'){
                        versionCmd += " --apps-branch ${getBuildBranch()}"
                    }
                    sh versionCmd
                }
            }
        }
        stage('Update Maven information') {
            steps {
                script {
                    // Update artifacts
                    updateArtifactCmd = "python3 scripts/update-maven-information.py"
                    if(getMavenArtifactRepository() != ''){
                        updateArtifactCmd += " --repo-url ${getMavenArtifactRepository()}"
                    }
                    if(getProjectVersion() != ''){
                        updateArtifactCmd += " --version ${getProjectVersion()}"
                    }
                    sh updateArtifactCmd

                    // Debug purpose in case of issue
                    sh "cat modules/kogito-data-index/module.yaml"
                    sh "cat modules/kogito-jobs-service/module.yaml"
                    sh "cat modules/kogito-management-console/module.yaml"
                }
            }
        }
        stage('Validate CeKit Image and Modules descriptors') {
            steps {
                sh """
                    curl -Ls https://github.com/kiegroup/kie-cloud-tools/releases/download/1.0-SNAPSHOT/cekit-image-validator-runner.tgz --output cekit-image-validator-runner.tgz
                    tar -xzvf cekit-image-validator-runner.tgz
                    chmod +x cekit-image-validator-runner
                """
                sh "./cekit-image-validator-runner modules/"
                sh """
                    ./cekit-image-validator-runner image.yaml
                    ./cekit-image-validator-runner kogito-data-index-overrides.yaml
                    ./cekit-image-validator-runner kogito-jobs-service-overrides.yaml
                    ./cekit-image-validator-runner kogito-management-console-overrides.yaml
                    ./cekit-image-validator-runner kogito-quarkus-jvm-overrides.yaml
                    ./cekit-image-validator-runner kogito-quarkus-overrides.yaml
                    ./cekit-image-validator-runner kogito-quarkus-s2i-overrides.yaml
                    ./cekit-image-validator-runner kogito-springboot-overrides.yaml
                    ./cekit-image-validator-runner kogito-springboot-s2i-overrides.yaml
                """
            }
        }
        stage('Commit Changes'){
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    // Commit changes before changing tests repository
                    githubscm.commitChanges("Update project version to ${getProjectVersion()} for release")
                } 
            }
        }
        stage('Setup tests repository') {
            when {
                // TODO set back
                //expression { return !shouldSkipTests() && getMavenArtifactRepository() != '' }
                expression { return getMavenArtifactRepository() != '' }
            }
            steps {
                script {
                    // Update repo in tests
                    sh "python3 scripts/update-tests-maven-repo.py --repo-url ${getMavenArtifactRepository()}"

                    // Debug purpose in case of issue
                    sh "cat tests/test-apps/clone-repo.sh"
                    sh "cat tests/features/kogito-quarkus-ubi8-s2i.feature"
                    sh "cat tests/features/kogito-springboot-ubi8-s2i.feature"
                }
            }
        }
        stage('Prepare offline kogito-examples') {
            when {
                expression { return !shouldSkipTests() }
            }
            steps {
                sh "make clone-repos"
            }
        }
        stage('Build and Test Images'){
            steps{
                script {
                    build_stages = [:]
                    IMAGES.each{ image -> build_stages[image] = {
                            initWorkspace(image)
                            dir(getWorkspacePath(image)){
                                try{
                                    sh "make ${image} ignore_test=${shouldSkipTests()} cekit_option='--work-dir .'"
                                }
                                finally{
                                    junit testResults: 'target/test/results/*.xml', allowEmptyResults: true
                                }
                            
                            }
                        }
                    }
                    parallel build_stages
                }
            }
        }
        stage('Tagging') {
            steps {
                script {
                    tagImages()
                }
            }
        }
        stage('Pushing') {
            steps {
                script {
                    if (isDeployImageInOpenshiftRegistry()) {
                        loginOpenshiftRegistry()
                    } else if (getDeployImageRegistryCredentials() != ''){
                        loginContainerRegistry(getDeployImageRegistry(), getDeployImageRegistryCredentials())
                    }
                    pushImages()
                }
            }
        }
        stage('Create PR')
        {
            when {
                expression {
                    return isRelease()
                }
            }
            steps{
                script{
                    githubscm.pushObject("origin", getProjectVersion(), env.BOT_CREDENTIALS_ID)
                    
                    def commitMsg = "Update project version to ${getProjectVersion()} for release"
                    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"
                    deployProperties["kogito-images.pr.link"] = githubscm.createPR(commitMsg, prBody, getBuildBranch(), env.BOT_CREDENTIALS_ID)
                }
            }
        }
    }
    post {
        always {
            script{
                def propertiesStr = deployProperties.collect{ entry -> "${entry.key}=${entry.value}" }.join("\n")
                writeFile( file : 'deployment.properties' , text : propertiesStr)
                archiveArtifacts artifacts: 'deployment.properties', allowEmptyArchive:true
                clean()
            }
        }
    }
}

void checkoutRepo() {
    checkout(githubscm.resolveRepository("kogito-images", getGitAuthor(), getBuildBranch(), false))
    // need to manually checkout branch since on a detached branch after checkout command
    sh "git checkout ${getBuildBranch()}"
}

void clean() {
    cleanWs()
    cleanImages()

    // Clean Cekit cache, in case we reuse an old node
    sh "rm -rf \$HOME/.cekit/cache"
}

void cleanImages(){
    sh "docker rm -f \$(docker ps -a -q) || date"
    sh "docker rmi -f \$(docker images -q) || date"
}

void tagImages() {
    for(String imageName : IMAGES) {
        sh "docker tag quay.io/kiegroup/${imageName}:latest ${buildImageName(imageName)}"
    }
}

void pushImages() {
    for(String imageName : IMAGES) {
        sh "docker push ${buildImageName(imageName)}"
    }
}

String buildImageName(String imageName) {
    String finalImageName = imageName
    if(getDeployImageNameSuffix() != ''){
        finalImageName += "-" + getDeployImageNameSuffix()
    }
    return "${getDeployImageRegistry()}/${getDeployImageNamespace()}/${finalImageName}:${getDeployImageTag()}"
}

void loginOpenshift(){
    withCredentials([usernamePassword(credentialsId: env.OPENSHIFT_CREDS_KEY, usernameVariable: 'OC_USER', passwordVariable: 'OC_PWD')]){
        sh "oc login --username=${OC_USER} --password=${OC_PWD} --server=${env.OPENSHIFT_API} --insecure-skip-tls-verify"
    }
}

void loginOpenshiftRegistry(){
    loginOpenshift()
    // username can be anything. See https://docs.openshift.com/container-platform/4.4/registry/accessing-the-registry.html#registry-accessing-directly_accessing-the-registry
    sh "set +x && docker login -u anything -p \$(oc whoami -t) ${env.OPENSHIFT_REGISTRY}"
}

void loginContainerRegistry(String registry, String credsId){
    withCredentials([usernamePassword(credentialsId: credsId, usernameVariable: 'REGISTRY_USER', passwordVariable: 'REGISTRY_PWD')]) {
        sh "docker login -u ${REGISTRY_USER} -p ${REGISTRY_PWD} ${registry}"
    }
}

void setDeployPropertyIfneeded(String key, def value){
    if(value != null && value != "") {
        deployProperties[key] = value
    }
}
////////////////////////////////////////////////////////////////////////
// Deploy image information
////////////////////////////////////////////////////////////////////////

boolean isDeployImageInOpenshiftRegistry(){
    return params.IMAGE_USE_OPENSHIFT_REGISTRY
}

String getDeployImageRegistryCredentials(){
    return isDeployImageInOpenshiftRegistry() ? "" : params.IMAGE_REGISTRY_CREDENTIALS
}

String getDeployImageRegistry(){
    return isDeployImageInOpenshiftRegistry() ? env.OPENSHIFT_REGISTRY : params.IMAGE_REGISTRY
}

String getDeployImageNamespace(){
    return isDeployImageInOpenshiftRegistry() ? "openshift" : params.IMAGE_NAMESPACE
}

String getDeployImageNameSuffix(){
    return params.IMAGE_NAME_SUFFIX
}

String getDeployImageTag(){
    if (params.IMAGE_TAG != ""){
        return params.IMAGE_TAG
    } else {
        return sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
    }
}


////////////////////////////////////////////////////////////////////////
// Workspaces
////////////////////////////////////////////////////////////////////////

void initWorkspace(String image){
    sh "mkdir -p ${getWorkspacePath(image)}"
    sh "rsync -av --progress . ${getWorkspacePath(image)} --exclude workspaces"
}

void cleanWorkspaces(){
    sh "rm -rf ${getWorkspacesPath()}"
}

String getWorkspacesPath(){
    return "${WORKSPACE}/workspaces"
}

String getWorkspacePath(String image){
    return "${getWorkspacesPath()}/${image}"
}

////////////////////////////////////////////////////////////////////////
// utils
////////////////////////////////////////////////////////////////////////

boolean isRelease() {
    return params.RELEASE
}

String getBuildBranch() {
    return params.BUILD_BRANCH_NAME
}

String getGitAuthor() {
    return params.GIT_AUTHOR
}

String getProjectVersion() {
    return params.PROJECT_VERSION
}

String getMavenArtifactRepository() {
    return params.MAVEN_ARTIFACT_REPOSITORY
}

boolean shouldSkipTests(){
    return params.SKIP_TESTS
}

void setDeployPropertyIfNeeded(String key, def value){
    if (value != null && value != ""){
        deployProperties[key] = value
    }
}